apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-android-extensions'


android {
  signingConfigs {
    config {
      keyAlias 'kpdo'
      keyPassword 'a123456'
      storeFile file('../kotlin_p_demo.jks')
      storePassword 'ktmp123456'
    }
  }


  compileSdkVersion rootProject.ext.android.compileSdkVersion
  defaultConfig {
    applicationId rootProject.ext.android.applicationId
    minSdkVersion rootProject.ext.android.minSdkVersion
    targetSdkVersion rootProject.ext.android.targetSdkVersion
    versionCode rootProject.ext.android.versionCode
    versionName rootProject.ext.android.versionName
    multiDexEnabled true
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    javaCompileOptions {
      annotationProcessorOptions {
        includeCompileClasspath true
      }
    }
    signingConfig signingConfigs.config
    // 实现毛玻璃那种透明的效果需要添加的库
    renderscriptTargetApi 19
    renderscriptSupportModeEnabled true // Enable RS support

    ndk {

      //APP的build.gradle设置支持的SO库架构
      abiFilters 'armeabi', 'armeabi-v7a', 'x86'
    }
  }
  buildTypes {
    debug {
      minifyEnabled false
      debuggable true
      signingConfig signingConfigs.config
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      debuggable false
      signingConfig signingConfigs.config
      zipAlignEnabled true
    }
  }


  // 自定义输出配置
  android.applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName = "kotlin_practise_v${variant.versionName}_${variant.name}.apk"
    }
  }

  compileOptions {
    targetCompatibility JavaVersion.VERSION_1_8
    sourceCompatibility JavaVersion.VERSION_1_8
  }
  productFlavors {
  }



  dexOptions {
    jumboMode true
  }
}
dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation ('com.android.support.test.espresso:espresso-core:3.0.1'){
    exclude group:'com.android.support', module: 'support-annotations'
  }

  //support
  implementation rootProject.ext.supportLibs

  //网络请求
  implementation rootProject.ext.networkLibs

  //Rxjava
  implementation rootProject.ext.rxjavaLibs

  //其他第三方
  implementation rootProject.ext.thirdLibs

  // APT dependencies(Kotlin内置的注解处理器)
  kapt rootProject.ext.annotationPrecessorLibs


  // 底部菜单
  implementation('com.flyco.tablayout:FlycoTabLayout_Lib:2.1.0@aar') {
    exclude group: 'com.android.support', module: 'support-v4'
  }

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
  implementation project(':multiple-status-view')
  //GlideOkHttp
  implementation(rootProject.ext.glideOkhttp) {
    exclude group: 'glide-parent'
  }


}
